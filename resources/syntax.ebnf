file = sections, ?TOKEN_EOF?;
sections = { section };
section = data_section | exec_section;

data_section = data_dir, data_stmts;
data_dir = ".DATA", ":", "\n";
data_stmts = { data_stmt };
data_stmt = byte_stmt | bytes_stmt | label_stmt;

byte_stmt = "byte", [ initializer ], "\n";
bytes_stmt = "bytes", "(", ?TOKEN_NUM?, ")", [ initializer ], "\n";
label_stmt = ?TOKEN_IDENT?, ":", "\n";
initializer = ?TOKEN_NUM? | ?TOKEN_ASCII? | byte_initializer;
byte_initializer = "{", numbers, "}";
numbers = [ ?TOKEN_NUM?, { ",", ?TOKEN_NUM? } ];

exec_section = exec_dir, exec_stmts;
exec_dir = ".EXEC", ":", "\n";
exec_stmts = { exec_stmt };
exec_stmt = instruction_stmt | macro_stmt | label_stmt | loc_label_stmt;
instruction_stmt = ?TOKEN_INSTR?, [ condition_code ], args, "\n";
condition_code = "(", ?TOKEN_COND_CODE?, ")";
args = [ arg, { ",", arg} ];
arg = ?TOKEN_REG? | ?TOKEN_SYS_REG? | ?TOKEN_ADDR_REG? | ?TOKEN_PORT? | immediate | label | loc_label;
immediate = "#", ?TOKEN_NUM?;
label = ?TOKEN_IDENT?;
loc_label = direction_dir, [loc_label_dist], ?TOKEN_IDENT?;
direction_dir = ".f" | ".b";
loc_label_dist = "(", ?TOKEN_NUM?, ")";

macro_stmt = ?TOKEN_MACRO?, [ condition_code ], args, "\n";
loc_label_stmt = ".l", ?TOKEN_IDENT?, ":", "\n";